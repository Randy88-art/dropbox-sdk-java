import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.jetbrains.kotlinx.binary-compatibility-validator"
    id "com.vanniktech.maven.publish"
    id "com.github.ben-manes.versions"
    id "com.dropbox.dependency-guard"
}

android {
    namespace = "com.dropbox.core.sdk.android"
    compileSdk dropboxJavaSdkLibs.versions.android.compile.sdk.get().toInteger()
    defaultConfig {
        minSdk dropboxJavaSdkLibs.versions.android.min.sdk.get().toInteger()
        targetSdk dropboxJavaSdkLibs.versions.android.target.sdk.get().toInteger()
    }

    kotlinOptions {
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
}

dependencies {
    api(project(path: ":core", configuration: "withoutOsgi"))
}

dependencyGuard {
    configuration("releaseRuntimeClasspath")
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01)
    signAllPublications()
}